buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'com.novoda:bintray-release:0.2.7'
    }
}

apply plugin: 'idea'
apply plugin: 'scala'
apply plugin: 'forge'
apply plugin: 'bintray-release'

version = "0.1-SNAPSHOT"
group = "com.easyforger"
archivesBaseName = "easyforger-core"

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
    scalaCompileOptions.additionalParameters = ["-language:implicitConversions"]
}

minecraft {
    version = "1.7.10-10.13.2.1291"
    runDir = "eclipse"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea {
    module {
        inheritOutputDirs = true
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.build.dependsOn('deobfJar')

// FIXME: publishing of the deobf artifact is not working
publish {
    bintrayUser = myBintrayUser
    bintrayKey = myBintrayKey

    userOrg = 'easyforger'
    groupId = project.group
    artifactId = archivesBaseName

    version = project.version
    description = 'A DSL / Framework for developing Minecraft Forge mods.'

    website = 'https://easyforger.com'
    issueTracker = 'https://github.com/easyforger/easyforger/issues'
    repository = 'git@github.com:easyforger/easyforger.git'

    dryRun = false
}
